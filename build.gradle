import org.gradle.api.tasks.testing.logging.TestExceptionFormat

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url 'https://dl.bintray.com/jetbrains/intellij-plugin-service'
        }

    }
    dependencies {
        classpath "org.jetbrains.intellij.plugins:gradle-intellij-plugin:0.5.0-SNAPSHOT"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.4.9"
    id 'com.palantir.git-version' version "0.11.0"
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

intellij {
    version ideaVersion
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            "com.jetbrains.twig:${twigPluginVersion}",
            'CSS',
            'java-i18n',
            'properties'
    ]
    pluginName 'PHP Toolbox'

    // Can't instantiate configurable for PHP Toolbox
    // at de.espend.idea.php.toolbox.ui.application.ToolboxApplicationForm.<init>(ToolboxApplicationForm.java:26)
    tasks {
        "buildSearchableOptions" {
            enabled = false
        }
    }
}

patchPluginXml {
    sinceBuild '191'
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

publishPlugin {
    username System.getenv('IJ_REPO_USERNAME')
    password System.getenv('IJ_REPO_PASSWORD')
}

group 'de.espend.idea.php.toolbox'

def details = versionDetails()
if (details.isCleanTag) {
    version = "${details.lastTag}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

wrapper {
    gradleVersion '5.5.1'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
