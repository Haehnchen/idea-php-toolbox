import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id "org.jetbrains.intellij" version "0.7.2"
    id 'com.palantir.git-version' version "0.12.3"
}

repositories {
    mavenCentral()
    jcenter()
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

intellij {
    version ideaVersion
    updateSinceUntilBuild false
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            "com.jetbrains.twig:${twigPluginVersion}",
            'CSS',
            'java-i18n',
            'properties',
            'java'
    ]
    pluginName 'PHP Toolbox'

    // Can't instantiate configurable for PHP Toolbox
    // at de.espend.idea.php.toolbox.ui.application.ToolboxApplicationForm.<init>(ToolboxApplicationForm.java:26)
    tasks {
        "buildSearchableOptions" {
            enabled = false
        }
    }
}

patchPluginXml {
    sinceBuild '211'
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

publishPlugin {
    token System.getenv('IJ_TOKEN')
}

group 'de.espend.idea.php.toolbox'

def details = versionDetails()
if (details.isCleanTag) {
    version = "${details.lastTag}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

wrapper {
    gradleVersion '7.0.0'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
